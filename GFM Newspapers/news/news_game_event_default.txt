generator_selector =
{
    type = "GAME_EVENT"
    name = "default"
    case = { value = 10 }
    case =
    {
        trigger = { news_printing_count = 1 }
        value = -999
    }
}

news_priority =
{
    type = "GAME_EVENT"
    name = "default"

    # Default priority of typical game event
    case = { priority_add = 10 }

    # More interested in events about inventing ideologies
    case =
    {
        trigger =
        {
            OR =
            {
                # Specific event IDs
                values_eq = { 0 0 1000 }
                values_eq = { 0 0 1001 }
                values_eq = { 0 0 1002 }
                values_eq = { 0 0 1003 }
                values_eq = { 0 0 169001 }
            }
        }
        priority_add = 100
    }

    # Bump up priority for any major event
    case =
    {
        trigger = { values_eq = { 0 1 1 } } # If major
        priority_add = 100
    }

    # Except those... (for example)
    #case =
    #{
    #    trigger =
    #    {
    #        # Specific event IDs and selected options
    #        OR =
    #        {
    #            AND = { values_eq = { 0 0 44100 }   values_eq = { 0 3 0 } }
    #            AND = { values_eq = { 0 0 44101 }   values_eq = { 0 3 0 } }
    #            AND = { values_eq = { 0 0 44102 }   values_eq = { 0 3 0 } }
    #        }
    #    }
    #    priority_add = 100
    #}
    # And even more in those...
    #case =
    #{
    #    trigger =
    #    {
    #        # Specific event IDs and selected options
    #        OR =
    #        {
    #            AND = { values_eq = { 0 0 44103 }   values_eq = { 0 3 0 } }
    #            AND = { values_eq = { 0 0 44104 }   values_eq = { 0 3 0 } }
    #            AND = { values_eq = { 0 0 44105 }   values_eq = { 0 3 0 } }
    #        }
    #    }
    #    priority_add = 200
    #}
    # A bit more interested in any sort of elections
    #case =
    #{
    #    trigger =
    #    {
    #        values_eq = { 0 2 1 }
    #    }
    #    priority_add = 10
    #}
    # etc...

    # And totally not interested in events that got duplicated (was already printed)
    case =
    {
        trigger = { news_printing_count = 1 }
        priority_add = -999
    }
}

generate_article =
{
    type = "GAME_EVENT"
    size = large
    name = "default"

    # Use the same picture as event
    picture_case =
    {
        # if province event (use general pics as those has no images defined)
        trigger = { values_eq = { 0 4 1 } }
        picture = "news/revolution_01_news_image.dds"
    }
    picture_case = { picture = "events/$STRING_0_1$.tga" }

    # Use the same title as event
    title_case = { text_add = { "$STRING_0_0$" } }

    # Generate description text (Use special key text generation for events)
    description_case = { text_add = { "$EVENT_DESC_0_0_LONG$" } } # If missing - will fallback to default text (the longest)
}

generate_article =
{
    type = "GAME_EVENT"
    size = medium
    name = "default"

    # Use the same picture as event
    picture_case =
    {
        # if province event (use general pics as those has no images defined)
        trigger = { values_eq = { 0 4 1 } }
        picture = "news/revolution_01_news_image.dds"
    }
    picture_case = { picture = "events/$STRING_0_1$.tga" }

    # Use the same title as event
    title_case = { text_add = { "$STRING_0_0$" } }

    # Generate description text (Use special key text generation for events)
    #description_case = { text_add = { "$EVENT_DESC_0_0_LONG$" } }
    description_case = { text_add = { "$EVENT_DESC_0_0_MEDIUM$" } } # If missing - will fallback to default text (the longest)
}

generate_article =
{
    type = "GAME_EVENT"
    size = small
    name = "default"

    # Use the same title as event
    title_case = { text_add = { "$STRING_0_0$" } }

    # Generate description text (Use special key text generation for events)
    #description_case = { text_add = { "$EVENT_DESC_0_0_LONG$" } }
    description_case = { text_add = { "$EVENT_DESC_0_0_SHORT$" } } # If missing - will fallback to default text (the longest)
}
